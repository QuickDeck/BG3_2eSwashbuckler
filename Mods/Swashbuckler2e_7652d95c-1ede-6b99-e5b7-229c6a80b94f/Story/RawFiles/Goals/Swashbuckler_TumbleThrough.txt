Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_TumbleThroughSpells("Target_TumbleThroughPanache");
DB_TumbleThroughSpells("Target_TheBiggerTheyAre");
KBSECTION
// If we cast Bigger They Are, add the target to a DB,
// clearing it out first
IF
UsingSpellOnTarget((GUIDSTRING)_Source, (GUIDSTRING)_Target, "Target_TheBiggerTheyAre", _, _, _)
AND
DB_TheBiggerTheyAre(_Source, (GUIDSTRING)_OldTarget)
THEN
NOT DB_TheBiggerTheyAre(_Source, _OldTarget);

IF
UsingSpellOnTarget((GUIDSTRING)_Source, (GUIDSTRING)_Target, "Target_TheBiggerTheyAre", _, _, _)
THEN
DB_TheBiggerTheyAre(_Source, _Target);

// If we passed through the enemy successfully, as
// indicated by the status, apply weakness to the
// target.
IF
StatusApplied((GUIDSTRING)_Source, "TUMBLE_THROUGH_PANACHE_COMPLETE", _, _)
AND
DB_TheBiggerTheyAre(_Source, (GUIDSTRING)_Target)
AND
HasActiveStatus(_Target, "THE_BIGGER_THEY_ARE_CRIT_MARKER", 1)
THEN
NOT DB_TheBiggerTheyAre(_Source, _Target);
RemoveStatus(_Target, "THE_BIGGER_THEY_ARE_CRIT_MARKER");
ApplyStatus(_Target, "THE_BIGGER_THEY_ARE_WEAKNESS_CRIT", 6.0);

IF
StatusApplied((GUIDSTRING)_Source, "TUMBLE_THROUGH_PANACHE_COMPLETE", _, _)
AND
DB_TheBiggerTheyAre(_Source, (GUIDSTRING)_Target)
AND
HasActiveStatus(_Target, "THE_BIGGER_THEY_ARE_CRIT_MARKER", 0)
THEN
NOT DB_TheBiggerTheyAre(_Source, _Target);
ApplyStatus(_Target, "THE_BIGGER_THEY_ARE_WEAKNESS", 6.0);
EXITSECTION

ENDEXITSECTION
