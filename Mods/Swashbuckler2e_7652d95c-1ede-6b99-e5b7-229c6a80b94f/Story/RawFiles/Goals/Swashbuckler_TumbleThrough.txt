Version 1
SubGoalCombiner SGC_AND
INITSECTION
DB_TumbleThroughSpells("Target_TumbleThroughPanache", "TUMBLE_THROUGH_PANACHE_CHECK");
DB_TumbleThroughSpells("Target_TheBiggerTheyAre", "THE_BIGGER_THEY_ARE_CHECK");
KBSECTION
//REGION Handles automated sequence of Tumble Through

IF
UsingSpellOnTarget((GUIDSTRING)_Player, (GUIDSTRING)_Target, (STRING)_Spell, _, _, (INTEGER)_ID)
AND
DB_TumbleThroughSpells(_Spell, (STRING)_Status)
THEN
DB_TumbleThroughEvents(_Player, _Target, _ID);
ApplyStatus(_Target, "ACROBATICS_ROLL_TO_REFLEX_DC", 6.0);
ApplyStatus(_Target, "ACROBATICS_ROLL_TO_REFLEX_DC_2", 6.0);
ApplyStatus(_Target, "ACROBATICS_ROLL_TO_REFLEX_DC_3", 6.0);

IF
CastedSpell((GUIDSTRING)_Player, (STRING)_Spell, _, _, (INTEGER)_ID)
AND
DB_TumbleThroughSpells(_Spell, (STRING)_Status)
AND
DB_TumbleThroughEvents(_Player, (GUIDSTRING)_Target, _ID)
AND
CalculatePassiveSkill((GUIDSTRING)_Target, "Acrobatics", (INTEGER)_Difficulty)
THEN
PROC_RequestSkillCheckStatus(_Status, _Difficulty, (CHARACTER)_Player, "TumbleThroughAcrobaticsCheck");
RemoveStatus(_Target, "ACROBATICS_ROLL_TO_REFLEX_DC");
RemoveStatus(_Target, "ACROBATICS_ROLL_TO_REFLEX_DC_2");
RemoveStatus(_Target, "ACROBATICS_ROLL_TO_REFLEX_DC_3");

IF
StatusApplied((GUIDSTRING)_Player, "THE_BIGGER_THEY_ARE_CASTER", _, _)
AND
DB_TumbleThroughEvents(_Player, (GUIDSTRING)_Target, (INTEGER)_ID)
THEN
ApplyStatus(_Target, "THE_BIGGER_THEY_ARE_WEAKNESS", 6.0);

IF
StatusApplied((GUIDSTRING)_Player, "THE_BIGGER_THEY_ARE_CASTER_CRIT", _, _)
AND
DB_TumbleThroughEvents(_Player, (GUIDSTRING)_Target, (INTEGER)_ID)
THEN
ApplyStatus(_Target, "THE_BIGGER_THEY_ARE_WEAKNESS_CRIT", 6.0);

IF
StatusApplied((GUIDSTRING)_Player, "TUMBLE_THROUGH_PANACHE_SUCCESS", _, _)
AND
DB_TumbleThroughEvents(_Player, (GUIDSTRING)_Target, (INTEGER)_ID)
AND
GetPosition(_Player, (REAL)_XP, (REAL)_YP, (REAL)_ZP)
AND
GetPosition(_Target, (REAL)_XT, (REAL)_YT, (REAL)_ZT)
AND
RealSubtract(_XT, _XP, (REAL)_XD)
AND
RealSubtract(_YT, _YP, (REAL)_YD)
AND
RealSubtract(_ZT, _ZP, (REAL)_ZD)
AND
RealProduct(_XD, 2.0, (REAL)_XDN)
AND
RealProduct(_YD, 2.0, (REAL)_YDN)
AND
RealProduct(_ZD, 2.0, (REAL)_ZDN)
AND
RealSum(_XDN, _XP, (REAL)_XPN)
AND
RealSum(_YDN, _YP, (REAL)_YPN)
AND
RealSum(_ZDN, _ZP, (REAL)_ZPN)
THEN
NOT DB_TumbleThroughEvents(_Player, _Target, _ID);
ApplyStatus(_Target, "TUMBLE_THROUGH_NO_COLLISION", 1.0, 0);
DB_JumpEventsTumbleThrough(_Player, _Target);
UseSpellAtPosition(_Player, "Projectile_JumpTumbleThrough", _XPN, _YPN, _ZPN, 1);

IF
StatusApplied((GUIDSTRING)_Player, "TUMBLE_THROUGH_PANACHE_FAIL", _, _)
AND
DB_TumbleThroughEvents(_Player, (GUIDSTRING)_Target, (INTEGER)_ID)
THEN
NOT DB_TumbleThroughEvents(_Player, _Target, _ID);
UseSpell(_Player, "Shout_ReactToMove", _Player, _Player, 0);

IF
StatusApplied((GUIDSTRING)_Player, "TUMBLE_THROUGH_PANACHE_CRITFAIL", _, _)
AND
DB_TumbleThroughEvents(_Player, (GUIDSTRING)_Target, (INTEGER)_ID)
THEN
NOT DB_TumbleThroughEvents(_Player, _Target, _ID);
UseSpell(_Player, "Shout_ReactToMove", _Player, _Player, 0);

IF
CastedSpell((GUIDSTRING)_Player, "Projectile_JumpTumbleThrough", _, _, _)
AND
DB_JumpEventsTumbleThrough(_Player, (GUIDSTRING)_Target)
THEN
RemoveStatus(_Target, "TUMBLE_THROUGH_NO_COLLISION");
NOT DB_JumpEventsTumbleThrough(_Player, _Target);

IF
TurnEnded(_)
AND
DB_TumbleThroughEvents((GUIDSTRING)_Player, (GUIDSTRING)_Target, (INTEGER)_ID)
THEN
NOT DB_TumbleThroughEvents(_Player, _Target, _ID);

//END_REGION
EXITSECTION

ENDEXITSECTION
